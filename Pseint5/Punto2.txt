SubProceso punto6()
	Definir Contactos como texto;
	Dimension Contactos[3,3];
	Definir C como entero;
	Definir F como entero;
	Para C<-0 hasta 2 Hacer
		Para F<-0 Hasta 2 Hacer
			Contactos[F,C]<-"";
		FinPara
	FinPara
	Definir Opcion como entero;
	Definir Opcion2 como entero;
	Definir NumeroTelefonico como texto;
	Definir Nombre como texto;
	Definir NombreEmpresa como texto;
	Definir R como logico; //comprueba si se repite
	Repetir
		Escribir "Contactos";
		Escribir "1. Agregar Contactos";
		Escribir "2. Buscar Contacto";
		Escribir "3. Borrar Contacto";
		Escribir "4. Cerrar programa";		
		Leer Opcion;
		Segun Opcion hacer 
			1:
				Limpiar Pantalla;
				Escribir "Cual es el telefono que desea agregar: ";
				Leer NumeroTelefonico;
				R<-falso;
				para F <- 0 Hasta 2 Hacer
					Si NumeroTelefonico=Contactos[F,0]       
						Entonces
						R<-Verdadero;
					FinSi
				FinPara
				Si R=Falso Entonces 
					Para F<-0 hasta 2 Hacer
						Si Contactos[F,0]="" Entonces
							Contactos[F,0]<-NumeroTelefonico;
							Escribir "Nombre: ";
							Leer Nombre;
							Contactos[F,1]<-Nombre;
							Escribir "Organización: ";
							Leer NombreEmpresa;
							Contactos[F,2]<-NombreEmpresa;
							F<-2;
							R<-Verdadero;
							Limpiar pantalla;
						FinSi
					FinPara
					
					Si R=Falso Entonces
						Escribir "Por favor borre un numero para poder guardar el nuevo";
						Esperar 5 Segundos;
					FinSi
				SiNo
					R<-Falso;
					Limpiar Pantalla;
					Escribir "Contacto ya esta registrado, por favor vuelva a seleccionar que desea hacer";
					Esperar 5 Segundos;
					Limpiar Pantalla;
				FinSi
			2:
				Limpiar Pantalla;
				Escribir "Buscar Contacto";
				Leer Nombre;
				R<-falso;
				Para F<-0 hasta 2 Hacer
					Si Contactos[F,1]=Nombre Entonces
						Escribir "Nombre de contacto: ",Contactos[F,1];
						Escribir "Número de documento: ",Contactos[F,0];
						Escribir "Organización: ",Contactos[F,2];
						Esperar 10 Segundos;
						R<-verdadero;
					FinSi
				FinPara
				Si R=falso Entonces
					Escribir "Datos no encontrados";
					Escribir "Contactos actuales:";
					Escribir "------------------------------";
					Para F<-0 hasta 2 Hacer
						Escribir"";
						Para C<-0 Hasta 2 Hacer
							Escribir Sin Saltar Contactos[F,C];
							Escribir Sin Saltar " - ";
						FinPara
					FinPara
					Esperar 10 Segundos;
				Finsi
				Limpiar Pantalla;
			3:
				Limpiar Pantalla;
				Escribir "Borrar contacto: ";
				Escribir"-------------------";
				Para F<-0 hasta 2 Hacer
					Escribir"";
					Escribir Sin Saltar F+1," - ";
					Para C<-0 Hasta 2 Hacer
						Escribir Sin Saltar Contactos[F,C]," - ";
					FinPara
				FinPara
				Escribir "";
				Escribir "Que numero de contacto desea borrar";
				Leer Opcion2;
				Segun Opcion2 Hacer
					1:
						Para C<-0 Hasta 2 Hacer
							Contactos[0,C]<-"";
						FinPara
					2:
						Para C<-0 Hasta 2 Hacer
							Contactos[1,C]<-"";
						FinPara
					3:
						Para C<-0 Hasta 2 Hacer
							Contactos[2,C]<-"";
						FinPara
					De Otro Modo:
						Escribir "Opcion no valida";
						Esperar 5 segundos;
				FinSegun
				Limpiar Pantalla;
		FinSegun       
	Hasta que Opcion=4;
FinSubProceso

SubProceso Punto7()
	Definir Marca, Placa, NombreCompleto, NumeroTelefono, Contactos Como Caracter;
	Definir C,F,Opcion como entero;
	Dimension Contactos[5,4];
	Para C<-0 hasta 3 Hacer
		Para F<-0 Hasta 4 Hacer
			Contactos[F,C]<-"";
		FinPara
	FinPara
	Repetir
		Escribir "El guardián";
		Escribir "--------------------";
		Escribir "1-Ingresar al parqueadero";
		Escribir "2-Retirar del parqueadero";
		Escribir "3-Consultar vehiculo";
		Escribir "4-Cerrar programa";
		Leer Opcion;
		Segun Opcion Hacer
			1:
				Limpiar Pantalla;
				Escribir "Ingreso al parqueadero";
				Escribir "--------------------------";
				Escribir "Número de placa: ";
				Leer Placa;
				Para F<-0 Hasta 4 Hacer
					Si Contactos[F,0]="" Entonces
						Escribir "Marca: ";
						Leer Marca;
						Escribir "Nombre completo del dueño";
						Leer NombreCompleto;
						Escribir "Número completo";
						Leer NumeroTelefono;
						Contactos[F,0]<-Placa;
						Contactos[F,1]<-Marca;
						Contactos[F,2]<-NombreCompleto;
						Contactos[F,3]<-NumeroTelefono;
						Escribir "vehiculo cargado";
						F<-4;
						Esperar Tecla;
						Limpiar Pantalla;
					FinSi
				FinPara
			2:
				Limpiar Pantalla;
				Escribir "Retirar del parqueadero";
				Escribir "____________________________";
				Escribir "Número de placa:";
				Leer Placa;
				Para F<-0 Hasta 4 Hacer
					Si Contactos[F,0]=Placa Entonces
						Contactos[F,0]<-"";
						Contactos[F,1]<-"";
						Contactos[F,2]<-"";
						Contactos[F,3]<-"";
						Escribir "Vehiculo Retirado";
						Placa<-"";
						F<-4;
						Esperar Tecla;
					FinSi
				FinPara
				Si Placa<>"" Entonces
					Escribir "Vehiculo no esta registrado en el inventario";
					Esperar Tecla;
				FinSi
				Limpiar Pantalla;
			3:
				Limpiar Pantalla;
				Escribir "Consultar vehiculo en el parqueadero";
				Escribir "_______________________________________";
				Escribir "Número de placa:";
				Leer Placa;
				Para F<-0 Hasta 4 Hacer
					Si Contactos[F,0]=Placa Entonces
						Placa<-"";
						Para C<-0 Hasta 3 Hacer
							Escribir Sin Saltar Contactos[F,C]," - ";
						FinPara
					FinSi
				FinPara
				Si Placa<>"" Entonces
					Escribir "Vehiculo no esta en el parqueadero";
					Escribir "Vehiculos en el parqueadero";
					Para F<-0 Hasta 4 Hacer
						Si Contactos[F,0]<>"" Entonces
							Escribir Sin Saltar F+1," - ";
							Para C<-0 Hasta 3 Hacer
								Escribir Sin Saltar Contactos[F,C]," - ";
							FinPara
						FinSi
					FinPara
				FinSi
				Escribir "";
				Esperar Tecla;
				Limpiar Pantalla;
			De Otro Modo:
				Escribir "Opcion no valida";
		FinSegun
	Hasta Que Opcion=4
FinSubProceso

SubProceso Punto8()
	Definir Nombre, Usuario, Licencia, Fecha, Estado, Grupo Como Caracter;
	Definir C,F,Opcion como entero;
	Dimension Grupo[8,5];
	Para C<-0 hasta 4 Hacer
		Para F<-0 Hasta 7 Hacer
			Grupo[F,C]<-"";
		FinPara
	FinPara
	Repetir
		Escribir "El Maestro";
		Escribir "--------------------";
		Escribir "1-Registrar al curso";
		Escribir "2-Consultar usuarios";
		Escribir "3-Resultados";
		Escribir "4-Cerrar programa";
		Leer Opcion;
		Segun Opcion Hacer
			1:
				Limpiar Pantalla;
				Escribir "Registrar usuario";
				Escribir "--------------------------";
				Escribir "Nombre: ";
				Leer Nombre;
				Para F<-0 Hasta 7 Hacer
					Si Grupo[F,0]="" Entonces
						Escribir "Documento: ";
						Leer Usuario;
						Escribir "Licencia a obtener: ";
						Leer Licencia;
						Escribir "Fecha: ";
						Leer Fecha;
						Grupo[F,0]<-Usuario;
						Grupo[F,1]<-Nombre;
						Grupo[F,2]<-Licencia;
						Grupo[F,3]<-Fecha;
						Grupo[F,4]<-"Sin examen";
						Escribir "Usuario creado";
						F<-7;
						Esperar Tecla;
						Limpiar Pantalla;
					FinSi
				FinPara
			2:
				Limpiar Pantalla;
				Escribir "Consultar usuario";
				Escribir "____________________________";
				Escribir "Número de documento:";
				Leer Usuario;
				Para F<-0 Hasta 7 Hacer
					Si Grupo[F,0]=Usuario Entonces
						Para C<-0 Hasta 4 Hacer
							Escribir Grupo[F,C], " - ";
						FinPara
						F<-7;
						Esperar Tecla;
						Usuario<-"";
					FinSi
				FinPara
				Si Usuario<>"" Entonces
					Escribir "Persona no esta registrada en el curso";
					Esperar Tecla;
				FinSi
				Limpiar Pantalla;
			3:
				Limpiar Pantalla;
				Escribir "Resultados";
				Escribir "_______________________________________";
				Escribir "Número de documento:";
				Leer Usuario;
				Para F<-0 Hasta 7 Hacer
					Si Grupo[F,0]=Usuario Entonces
						Para C<-0 Hasta 4 Hacer
							Escribir Sin Saltar Grupo[F,C]," - ";
						FinPara
						Escribir "";
						Escribir "Usuario aprueba curso: ";
						Escribir "1-Si";
						Escribir "2-No";
						Leer Opcion;
						Segun Opcion Hacer
							1:
								Grupo[F,4]<-"Aprobado";
							2: 
								Grupo[F,4]<-"No aprobado";
							De Otro Modo:
								Escribir "No valido";
						FinSegun
						F<-7;
						Usuario<-"";
					FinSi
				FinPara
				Si Usuario<>"" Entonces
					Escribir "Persona no esta registrada en el curso";
				FinSi
				Escribir "";
				Esperar Tecla;
				Limpiar Pantalla;
			De Otro Modo:
				Escribir "Opcion no valida";
		FinSegun
	Hasta Que Opcion=4
FinSubProceso

Proceso Punto2
	Definir Opcion Como Entero;
	Definir i Como Entero;
	
	Para i<-0 Hasta 20 Hacer
		Limpiar Pantalla;
		Escribir "Punto a validar";
		Escribir "1-Punto 6";
		Escribir "2-Punto 7";
		Escribir "3-Punto 8";
		Escribir "4-Salir del programa";
		Leer Opcion;
		Segun Opcion Hacer
			1:
				Punto6;
				i<-2;
			2:
				Punto7;
				i<-2;
			3:
				Punto8;
				i<-2;
			4:
				i<-20;
			De Otro Modo:
				Escribir "Opcion no valida";
				i<-1;
				Esperar Tecla;
		FinSegun
	FinPara
FinProceso
